!     SUBROUTINE EZFFTF(N,R,AZERO,A,B,WSAVE)
!
!     SUBROUTINE EZFFTF COMPUTES THE FOURIER COEFFICIENTS OF A REAL
!     PERODIC SEQUENCE (FOURIER ANALYSIS). THE TRANSFORM IS DEFINED
!     BELOW AT OUTPUT PARAMETERS AZERO,A AND B. EZFFTF IS A SIMPLIFIED
!     BUT SLOWER VERSION OF RFFTF.
!
!     INPUT PARAMETERS
!
!     N       THE LENGTH OF THE ARRAY R TO BE TRANSFORMED.  THE METHOD
!             IS MUST EFFICIENT WHEN N IS THE PRODUCT OF SMALL PRIMES.
!
!     R       A REAL ARRAY OF LENGTH N WHICH CONTAINS THE SEQUENCE
!             TO BE TRANSFORMED. R IS NOT DESTROYED.
!
!     WSAVE   A WORK ARRAY WHICH MUST BE DIMENSIONED AT LEAST 3*N+15.
!             IN THE PROGRAM THAT CALLS EZFFTF. THE WSAVE ARRAY MUST BE
!             INITIALIZED BY CALLING SUBROUTINE EZFFTI(N,WSAVE) AND A
!             DIFFERENT WSAVE ARRAY MUST BE USED FOR EACH DIFFERENT
!             VALUE OF N. THIS INITIALIZATION DOES NOT HAVE TO BE
!             REPEATED SO LONG AS N REMAINS UNCHANGED THUS SUBSEQUENT
!             TRANSFORMS CAN BE OBTAINED FASTER THAN THE FIRST.
!             THE SAME WSAVE ARRAY CAN BE USED BY EZFFTF AND EZFFTB.
!
!     OUTPUT PARAMETERS
!
!     AZERO   THE SUM FROM I=1 TO I=N OF R(I)/N
!
!     A,B     FOR N EVEN B(N/2)=0. AND A(N/2) IS THE SUM FROM I=1 TO
!             I=N OF (-1)**(I-1)*R(I)/N
!
!             FOR N EVEN DEFINE KMAX=N/2-1
!             FOR N ODD  DEFINE KMAX=(N-1)/2
!
!             THEN FOR  K=1,...,KMAX
!
!                  A(K) EQUALS THE SUM FROM I=1 TO I=N OF
!
!                       2./N*R(I)*COS(K*(I-1)*2*PI/N)
!
!                  B(K) EQUALS THE SUM FROM I=1 TO I=N OF
!
!                       2./N*R(I)*SIN(K*(I-1)*2*PI/N)
!
!
SUBROUTINE ezfftf (N,R,AZERO,A,B,WSAVE)

    DIMENSION R(*), A(*), B(*), WSAVE(*)

  IF (N-2 .LT. 0) THEN
    AZERO = R(1)
  ELSE IF (N-2 .EQ. 0) THEN
    AZERO = .5*(R(1)+R(2))
    A(1) = .5*(R(1)-R(2))
  ELSE !(N-2 .GT. 0)
    DO I=1,N
      WSAVE(I) = R(I)
    END DO
    CALL rfftf (N,WSAVE,WSAVE(N+1))
    CF = 2./FLOAT(N)
    CFM = -CF
    AZERO = .5*CF*WSAVE(1)
    NS2 = (N+1)/2
    NS2M = NS2-1
    DO I=1,NS2M
      A(I) = CF*WSAVE(2*I)
      B(I) = CFM*WSAVE(2*I+1)
    END DO
    IF (MOD(N,2) .EQ. 1) THEN
      CONTINUE
    ELSE
      A(NS2) = .5*CF*WSAVE(N)
      B(NS2) = 0.
    END IF
  END IF

RETURN
END SUBROUTINE ezfftf
