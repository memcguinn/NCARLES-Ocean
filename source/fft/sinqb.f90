!     SUBROUTINE SINQB(N,X,WSAVE)
!
!     SUBROUTINE SINQB COMPUTES THE FAST FOURIER TRANSFORM OF QUARTER
!     WAVE DATA. THAT IS , SINQB COMPUTES A SEQUENCE FROM ITS
!     REPRESENTATION IN TERMS OF A SINE SERIES WITH ODD WAVE NUMBERS.
!     THE TRANSFORM IS DEFINED BELOW AT OUTPUT PARAMETER X.
!
!     SINQF IS THE UNNORMALIZED INVERSE OF SINQB SINCE A CALL OF SINQB
!     FOLLOWED BY A CALL OF SINQF WILL MULTIPLY THE INPUT SEQUENCE X
!     BY 4*N.
!
!     THE ARRAY WSAVE WHICH IS USED BY SUBROUTINE SINQB MUST BE
!     INITIALIZED BY CALLING SUBROUTINE SINQI(N,WSAVE).
!
!
!     INPUT PARAMETERS
!
!     N       THE LENGTH OF THE ARRAY X TO BE TRANSFORMED.  THE METHOD
!             IS MOST EFFICIENT WHEN N IS A PRODUCT OF SMALL PRIMES.
!
!     X       AN ARRAY WHICH CONTAINS THE SEQUENCE TO BE TRANSFORMED
!
!     WSAVE   A WORK ARRAY WHICH MUST BE DIMENSIONED AT LEAST 3*N+15.
!             IN THE PROGRAM THAT CALLS SINQB. THE WSAVE ARRAY MUST BE
!             INITIALIZED BY CALLING SUBROUTINE SINQI(N,WSAVE) AND A
!             DIFFERENT WSAVE ARRAY MUST BE USED FOR EACH DIFFERENT
!             VALUE OF N. THIS INITIALIZATION DOES NOT HAVE TO BE
!             REPEATED SO LONG AS N REMAINS UNCHANGED THUS SUBSEQUENT
!             TRANSFORMS CAN BE OBTAINED FASTER THAN THE FIRST.
!
!     OUTPUT PARAMETERS
!
!     X       FOR I=1,...,N
!
!                  X(I)= THE SUM FROM K=1 TO K=N OF
!
!                    4*X(K)*SIN((2K-1)*I*PI/(2*N))
!
!                  A CALL OF SINQB FOLLOWED BY A CALL OF
!                  SINQF WILL MULTIPLY THE SEQUENCE X BY 4*N.
!                  THEREFORE SINQF IS THE UNNORMALIZED INVERSE
!                  OF SINQB.
!
!     WSAVE   CONTAINS INITIALIZATION CALCULATIONS WHICH MUST NOT
!             BE DESTROYED BETWEEN CALLS OF SINQB OR SINQF.
!
SUBROUTINE sinqb (N,X,WSAVE)

    DIMENSION X(*), WSAVE(*)

  IF (N .GT. 1) THEN
    NS2 = N/2
    DO K=2,N,2
      X(K) = -X(K)
    END DO
    CALL cosqb (N,X,WSAVE)
    DO K=1,NS2
      KC = N-K
      XHOLD = X(K)
      X(K) = X(KC+1)
      X(KC+1) = XHOLD
    END DO
  ELSE
    X(1) = 4.*X(1)
  END IF

RETURN
END SUBROUTINE sinqb
