!     SUBROUTINE COST(N,X,WSAVE)
!
!     SUBROUTINE COST COMPUTES THE DISCRETE FOURIER COSINE TRANSFORM
!     OF AN EVEN SEQUENCE X(I). THE TRANSFORM IS DEFINED BELOW AT OUTPUT
!     PARAMETER X.
!
!     COST IS THE UNNORMALIZED INVERSE OF ITSELF SINCE A CALL OF COST
!     FOLLOWED BY ANOTHER CALL OF COST WILL MULTIPLY THE INPUT SEQUENCE
!     X BY 2*(N-1). THE TRANSFORM IS DEFINED BELOW AT OUTPUT PARAMETER X
!
!     THE ARRAY WSAVE WHICH IS USED BY SUBROUTINE COST MUST BE
!     INITIALIZED BY CALLING SUBROUTINE COSTI(N,WSAVE).
!
!     INPUT PARAMETERS
!
!     N       THE LENGTH OF THE SEQUENCE X. N MUST BE GREATER THAN 1.
!             THE METHOD IS MOST EFFICIENT WHEN N-1 IS A PRODUCT OF
!             SMALL PRIMES.
!
!     X       AN ARRAY WHICH CONTAINS THE SEQUENCE TO BE TRANSFORMED
!
!     WSAVE   A WORK ARRAY WHICH MUST BE DIMENSIONED AT LEAST 3*N+15
!             IN THE PROGRAM THAT CALLS COST. THE WSAVE ARRAY MUST BE
!             INITIALIZED BY CALLING SUBROUTINE COSTI(N,WSAVE) AND A
!             DIFFERENT WSAVE ARRAY MUST BE USED FOR EACH DIFFERENT
!             VALUE OF N. THIS INITIALIZATION DOES NOT HAVE TO BE
!             REPEATED SO LONG AS N REMAINS UNCHANGED THUS SUBSEQUENT
!             TRANSFORMS CAN BE OBTAINED FASTER THAN THE FIRST.
!
!     OUTPUT PARAMETERS
!
!     X       FOR I=1,...,N
!
!                 X(I) = X(1)+(-1)**(I-1)*X(N)
!
!                  + THE SUM FROM K=2 TO K=N-1
!
!                      2*X(K)*COS((K-1)*(I-1)*PI/(N-1))
!
!                  A CALL OF COST FOLLOWED BY ANOTHER CALL OF
!                  COST WILL MULTIPLY THE SEQUENCE X BY 2*(N-1)
!                  HENCE COST IS THE UNNORMALIZED INVERSE
!                  OF ITSELF.
!
!     WSAVE   CONTAINS INITIALIZATION CALCULATIONS WHICH MUST NOT BE
!             DESTROYED BETWEEN CALLS OF COST.
!
SUBROUTINE cost (N,X,WSAVE)

    DIMENSION X(*), WSAVE(*)

  NM1 = N-1
  NP1 = N+1
  NS2 = N/2
  IF (N-2 .LT. 0) THEN
    CONTINUE
  ELSE IF (N-2 .EQ. 0) THEN
    X1H = X(1)+X(2)
    X(2) = X(1)-X(2)
    X(1) = X1H
  ELSE !(N-2 .GT. 0)
    IF (N .GT. 3) THEN
      C1 = X(1)-X(N)
      X(1) = X(1)+X(N)
      DO K=2,NS2
        KC = NP1-K
        T1 = X(K)+X(KC)
        T2 = X(K)-X(KC)
        C1 = C1+WSAVE(KC)*T2
        T2 = WSAVE(K)*T2
        X(K) = T1-T2
        X(KC) = T1+T2
      END DO
      MODN = MOD(N,2)
      IF (MODN .NE. 0) X(NS2+1) = X(NS2+1)+X(NS2+1)
      CALL rfftf (NM1,X,WSAVE(N+1))
      XIM2 = X(2)
      X(2) = C1
      DO I=4,N,2
        XI = X(I)
        X(I) = X(I-2)-X(I-1)
        X(I-1) = XIM2
        XIM2 = XI
      END DO
      IF (MODN .NE. 0) X(N) = XIM2
    ELSE
      X1P3 = X(1)+X(3)
      TX2 = X(2)+X(2)
      X(2) = X(1)-X(3)
      X(1) = X1P3+TX2
      X(3) = X1P3-TX2
    END IF
  END IF

RETURN
END SUBROUTINE cost
